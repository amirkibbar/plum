plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    jcenter()
    maven { url "http://repo.spring.io/milestone" }
    maven { url "https://bintray.com/artifact/download/orbitz/consul-client" }
}

dependencies {
    compile "org.springframework.boot:spring-boot:1.2.3.RELEASE"
    compile "org.springframework.boot:spring-boot-autoconfigure:1.2.3.RELEASE"
    compile "org.springframework.retry:spring-retry:1.1.2.RELEASE"
    compile "com.orbitz.consul:consul-client:0.9.4"
    compile "dnsjava:dnsjava:2.1.7"
    compile "commons-io:commons-io:2.4"

    // the following are required to satisfy the orbitz consul client
    compile "org.apache.cxf:cxf-rt-rs-client:3.0.3"
    compile "org.apache.cxf:cxf-rt-transports-http-hc:3.0.3"
}

version = "0.1.9"

jar {
    baseName = 'plum'
    manifest {
        attributes("Implementation-Title": "consul4spring (plum)",
                "Implementation-version": project.version)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'ajk.plum'
            artifactId 'plum'
            version project.version

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'plum'
                    inceptionYear "2015"
                    url "https://github.com/amirkibbar/plum"
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/plum-${project.version}.pom")
    }
}

task install(dependsOn: 'publishMavenPublicationToMavenLocal') << {
    logger.info "installing $project.name"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    version = project.version

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    version = project.version

    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ['maven']

    publish = true

    pkg {
        repo = 'maven'
        name = 'consul4spring'
        websiteUrl = 'https://github.com/amirkibbar/plum'
        issueTrackerUrl = 'https://github.com/amirkibbar/plum/issues'
        vcsUrl = 'https://github.com/amirkibbar/plum.git'
        licenses = ['Apache-2.0']
        labels = ['spring-boot', 'consul']
        version {
            name = project.version
        }
    }
}

bintrayUpload.dependsOn = [sourcesJar, javadocJar]