apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    jcenter()
    maven { url "http://repo.spring.io/milestone" }
    maven { url "https://bintray.com/artifact/download/orbitz/consul-client" }
}

dependencies {
    compile("org.springframework.boot:spring-boot:1.2.3.RELEASE")
    compile("org.springframework.boot:spring-boot-autoconfigure:1.2.3.RELEASE")
    compile("com.orbitz.consul:consul-client:0.8.3")
    compile("dnsjava:dnsjava:2.1.7")
    compile("commons-io:commons-io:2.4")
}

def buildNum = System.getProperty("BUILD_NUMBER", "0")
def plumVer = "0.0.3"

jar {
    baseName = 'plum'
    version = plumVer
    manifest {
        attributes("Implementation-Title": "consul4spring (plum)",
                "Implementation-version": "${plumVer}-b$buildNum")
    }
}

task pomXml << {
    pom {
        project {
            groupId "ajk.plum"
            inceptionYear '2015'
            version plumVer
            url "https://github.com/amirkibbar/plum"

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }.writeTo("$buildDir/libs/plum-${plumVer}.pom")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'ajk.plum'
            artifactId 'plum'
            version plumVer

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'plum'
                    inceptionYear "2015"
                    url "https://github.com/amirkibbar/plum"
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/plum-${plumVer}.pom")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    version = plumVer

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    version = plumVer

    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}